schema
    route
        id string
        source string
        destination string
        path string[]
        sections string[]
        route_signals string[]
        conflicts string[]
        point_positions string[]
        length float
        train string
    end

    segment
        id string
        length float
    end

    signal
        id string
        type string
    end

    point
        id string
        segment string
    end

    train
        id string
        type string
        weight float
        length float
    end

    block
        id string
        segment string
        direction string
        length float
        limit int
        train_types string[]
        overlaps string[]
        block_signals string[]
    end

    crossing
        id string
        segment string
    end

    composition
        id string
        entry string
        exit string
        block string
        distant string
    end
end

config
    length
    weight
    type
    segment
    limit
end

signaltypes
    entry # Einfahrsignal
        aspects 
            yellow 0x02 
            green 0x01
            red 0x00
        end 
        initial red
    exit # Ausfahrsignal
        aspects
            yellow 0x02
            green 0x01
            red 0x00
        end
        initial red
    distant # Vorsignal
        aspects
            yellow 0x02
            green 0x01
        end
        initial yellow
    block # Blocksignal
        aspects
            green 0x01
            red 0x00
        end
        initial red
    lightstop # Licht-Sperrsignal
        aspects
            white 0x01
            red 0x00
        end
        initial red
    peripheral 
        aspects 
            on 0x00
        end
        initial on
    platformlight 
        aspects
            on 0x00
            off 0x01
        end 
        initial on
end

def get_shortest_route(string route_ids[]): string
    if route_ids.len > 0
        float min_len = 2147483647
        string result = route_ids[0]
        for string route_id in route_ids
            float length = get config route.length route_id
            if length < min_len
                min_len = length
                result = route_id
            end
        end

        return result
    end

    return ""
end

def get_previous_block(string route_id, string block_id): string
    string block_ids[] = get config route.sections route_id
    if block_ids.len < 2
        return ""
    end

    string pre_block = block_ids[0]
    int i = 1
    while i < block_ids.len
        if block_ids[i] == block_id
            break
        end

        pre_block = block_ids[i]
    end

    return pre_block
end

def get_block(string route_id, string segment_ids[]) : string
    if segment_ids.len == 0
        return ""
    end

    # get first segment
    string seg = segment_ids[0]

    # get all blocks and check
    string block_ids[] = get config route.sections route_id
    for string id in block_ids

        # check main segment
        string main_seg = get config block.segment id
        if main_seg == seg
            return id
        end
    end

    return ""
end

def is_block_occupied(string block_id): bool
    string main_seg = get config block.segment block_id
    if is main_seg occupied
        return true
    end

    string overlaps[] = get config block.overlaps block_id
    for string segment_id in overlaps
        if is segment_id occupied
            return true
        end
    end

    return false
end

def is_composition_signal(string id): bool
    return get config composition.id id == id
end